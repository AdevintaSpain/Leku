apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "io.gitlab.arturbosch.detekt"

ext["PUBLISH_ARTIFACT_ID"] = "leku"

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
  compileSdkVersion 32
  resourcePrefix 'leku_'

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 32
    multiDexEnabled true
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles 'lib-proguard-rules.txt'
    vectorDrawables.useSupportLibrary = true
  }

  flavorDimensions 'tier'

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
  }
  packagingOptions {
    resources {
      excludes += ['protobuf.meta']
    }
  }
  lint {
    disable 'ObsoleteLintCustomCheck'
  }
  namespace 'com.adevinta.leku'
}

detekt {
  buildUponDefaultConfig = true
  allRules = false
  config = files("$rootDir/config/detekt/detekt-config.yml")

  reports {
    html.enabled = true
    xml.enabled = true
    txt.enabled = true
    sarif.enabled = true
  }
}

ktlint {
  reporters {
	reporter "plain"
	reporter "checkstyle"
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  implementation "com.google.android.material:material:1.6.0-beta01"
  implementation 'androidx.fragment:fragment-ktx:1.4.1'

  def playServicesVersion = '18.0.2'
  implementation ("com.google.android.gms:play-services-maps:$playServicesVersion") {
	  exclude group: "com.android.support"
  }
  implementation ("com.google.android.gms:play-services-location:19.0.1") {
	  exclude group: "com.android.support"
  }

  implementation ("com.google.android.libraries.places:places:2.6.0") {
    exclude group: "com.android.support"
  }

  implementation "com.google.maps:google-maps-services:0.2.9"

  def espressoVersion = '3.4.0'
  androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
  androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"

  def supportTestVersion = '1.4.0'
  androidTestImplementation "androidx.test:runner:$supportTestVersion"
  androidTestImplementation "androidx.test:rules:$supportTestVersion"
  androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
  androidTestImplementation 'org.mockito:mockito-core:4.0.0'

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "io.gitlab.arturbosch.detekt"

ext["PUBLISH_ARTIFACT_ID"] = "leku"

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
  compileSdkVersion 31
  resourcePrefix 'leku_'

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 31
    multiDexEnabled true
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles 'lib-proguard-rules.txt'
    vectorDrawables.useSupportLibrary = true
  }

  flavorDimensions 'tier'

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
  }
  packagingOptions {
    exclude 'protobuf.meta'
  }
  lintOptions {
    disable 'ObsoleteLintCustomCheck'
  }
}

detekt {
  buildUponDefaultConfig = true
  allRules = false
  config = files("$rootDir/config/detekt/detekt-config.yml")

  reports {
    html.enabled = true
    xml.enabled = true
    txt.enabled = true
    sarif.enabled = true
  }
}

ktlint {
  reporters {
	reporter "plain"
	reporter "checkstyle"
  }
}

configurations.all {
  // This is a workaround for the issue (remove when androidx exits alpha libraries):
  // https://stackoverflow.com/questions/37347326/errorconflict-with-dependency-com-google-code-findbugsjsr305
  exclude group: 'com.google.code.findbugs', module: 'jsr305'
  exclude group: 'com.google.guava', module: 'listenablefuture'
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  implementation "com.google.android.material:material:1.5.0-beta01"
  implementation 'androidx.fragment:fragment-ktx:1.3.6'

  def playServicesVersion = '17.0.1'
  implementation ("com.google.android.gms:play-services-maps:$playServicesVersion") {
	  exclude group: "com.android.support"
  }
  implementation ("com.google.android.gms:play-services-location:18.0.0") {
	  exclude group: "com.android.support"
  }

  implementation ("com.google.android.libraries.places:places:2.5.0") {
    exclude group: "com.android.support"
  }

  implementation "com.google.maps:google-maps-services:0.2.9"

  implementation 'io.reactivex.rxjava3:rxjava:3.1.2'
  implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
  implementation 'nl.littlerobots.rxlint:rxlint:1.7.7'


  def espressoVersion = '3.4.0'
  androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
  androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"

  def supportTestVersion = '1.4.0'
  androidTestImplementation "androidx.test:runner:$supportTestVersion"
  androidTestImplementation "androidx.test:rules:$supportTestVersion"
  androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
  androidTestImplementation 'org.mockito:mockito-core:4.0.0'

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
